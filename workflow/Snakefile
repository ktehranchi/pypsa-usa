from snakemake.utils import min_version

min_version("6.0")

from shutil import copyfile, move, rmtree
from snakemake.remote.HTTP import RemoteProvider as HTTPRemoteProvider

HTTP = HTTPRemoteProvider()

from os.path import normpath
from pathlib import Path


# --------------------------- Workflow constraints --------------------------- #
localrules:
    dag,
    clean,


wildcard_constraints:
    interconnect="usa|texas|western|eastern",
    simpl="[a-zA-Z0-9]*|all",
    clusters="[0-9]+m?|all",
    ll="(v|c)([0-9\.]+|opt|all)|all",
    opts="[-+a-zA-Z0-9\.]*",
    scope="urban|rural|total",
    sector="([EG]-)*[EG]",


# -------------------------- Config and Subworkflows ------------------------- #


# Merge subworkflow configs and main config
configfile: "config/tests/config.validation.yaml"
configfile: "config/config.cluster.yaml"
configfile: "config/config.osw.yaml"
configfile: "config/config.plotting.yaml"


run = config.get("run", {})
RDIR = run["name"] + "/" if run.get("name") else ""
CDIR = RDIR if not run.get("shared_cutouts") else ""

LOGS = "logs/" + RDIR
BENCHMARKS = "benchmarks/" + RDIR
DATA = "data/"
RESOURCES = "resources/" + RDIR if not run.get("shared_resources") else "resources/"
RESULTS = "results/" + RDIR


include: "rules/common.smk"
include: "rules/retrieve.smk"
include: "rules/build_electricity.smk"
include: "rules/build_sector.smk"
include: "rules/solve_electricity.smk"
include: "rules/postprocess.smk"
include: "rules/validate.smk"
include: "rules/reliability.smk"


if "E" not in config["scenario"]["sector"]:
    if not config["scenario"]["sector"]:
        config["scenario"]["sector"] = "E"
    else:
        config["scenario"]["sector"] = f"E-{config['scenario']['sector']}"

if config["run"]["validation"]:
    COLLECT_FIGURES = FIGURES_VALIDATE
else:
    COLLECT_FIGURES = FIGURES_STATS + FIGURES_MAPS

# ----------------------------------- Rules ---------------------------------- #


rule all:
    input:
        # expand(
        #     RESULTS
        #     + "{interconnect}/figures/cluster_{clusters}/l{ll}_{opts}_{sector}/{figure}.pdf",
        #     **config["scenario"],
        #     figure=COLLECT_FIGURES,
        # ),
        expand(
            RESULTS
            + "{interconnect}/c{clusters}_ec_l{ll}_{opts}_{sector}/reliability_results.csv",
            **config["scenario"],
        ),
        "repo_data/dag.jpg",


# Create DAG with-
# snakemake --rulegraph all | sed -n "/digraph/,\$p" | dot -Tpng -o repo_data/dag.jpg
# snakemake --dag -F | sed -n "/digraph/,\$p" | dot -Tpng -o repo_data/dag.jpg
rule dag:
    message:
        "Creating DAG of workflow."
    output:
        dot="repo_data/dag.dot",
        jpg="repo_data/dag.jpg",
    shell:
        """
        snakemake --rulegraph all | sed -n "/digraph/,\$p" > {output.dot}
        dot -Tjpg -o {output.jpg} {output.dot}
        """


rule clean:
    message:
        "Remove all build results but keep downloaded data."
    run:
        import shutil

        shutil.rmtree("resources", ignore_errors=True)
        shutil.rmtree("results", ignore_errors=True)
        print("Data downloaded to data/ has not been cleaned.")
